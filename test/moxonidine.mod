$PROBLEM MOXONIDINE PK, FINAL ESTIMATES, ALL DATA
;;
;;
$INPUT      ID VISI DAT1=DROP DGRP DOSE FLAG=DROP ONO=DROP
  XIME=DROP DVO=DROP NEUY SCR AGE SEX NYH=DROP WT DROP ACE
  DIG DIU NUMB=DROP TAD TIME VIDD=DROP CRCL AMT SS II DROP
  CMT CONO=DROP DV EVID OVID=DROP DROP SHR2=DROP
  NYHA2
$DATA       mx19.csv IGNORE=@ IGNORE(VISI.EQ.8) IGNORE(DGRP.GE.8)

$SUBROUTINE ADVAN2 TRANS1
$PK

;-----------OCCASIONS----------
  VIS3               = 0
  IF(VISI.EQ.3) VIS3 = 1
  VIS8               = 0
  IF(VISI.EQ.8) VIS8 = 1

;----------IOV--------------------

  KPCL  = VIS3*ETA(4)+VIS8*ETA(5)
  KPKA  = VIS3*ETA(6)+VIS8*ETA(7)

;---------- PK model ------------------

  TVCL  = THETA(1)
  TVV   = THETA(2)

  CL    = TVCL*EXP(ETA(1)+KPCL)
  V     = TVV*EXP(ETA(2))
  KA    = THETA(3)*EXP(ETA(3)+KPKA)
  ALAG1 = THETA(4)
  K     = CL/V
  S2    = V

$ERROR

  IPRED = LOG(.025)
  WA     = THETA(5)
  W      = WA
  IF(F.GT.0) IPRED = LOG(F)
  IRES  = DV-IPRED
  IWRES = IRES/W
  Y     = IPRED+ERR(1)*W

$THETA  (0,27.6)             ;TVCL
$THETA  (0,85)             ;TVV
$THETA  (0,2.45)             ;TVKA
$THETA  (0,.140)             ;LAG
$THETA  (0,.33)              ;RES ERR

$OMEGA  BLOCK(2) 0.0444 0.027 0.0241    ; IIV (CL-V)
$OMEGA  BLOCK(1) 3.0           ; IIV KA

$OMEGA  BLOCK(1) 0.0165           ; IOV CL
$OMEGA  BLOCK(1)  SAME         ; IOV CL

$OMEGA  BLOCK(1)  0.495           ; IOV KA
$OMEGA  BLOCK(1)  SAME         ; IOV KA

$SIGMA  1  FIX

$ESTIMATION METHOD=1  MAXEVALS=9999
$COV MATRIX=R PRINT=E
$TABLE      ID TIME DV VISI DGRP EVID DOSE AGE SEX WT TAD CRCL AMT CMT
  IPRED PRED W ETA1 ETA2 ETA3 ETA4 ETA5 ETA6 ETA7 NOAPPEND NOPRINT ONEHEADER FILE=sdtab1
